import mods.gregtech.multiblock.Builder;
import mods.gregtech.multiblock.FactoryBlockPattern;
import mods.gregtech.multiblock.RelativeDirection;
import mods.gregtech.multiblock.IBlockMatcher;
import mods.gregtech.multiblock.MultiblockAbility;
import mods.gregtech.multiblock.FactoryMultiblockShapeInfo;
import mods.gregtech.multiblock.IBlockInfo;

import mods.gregtech.MetaTileEntities;

import mods.gregtech.recipe.FactoryRecipeMap;

import crafttweaker.world.IFacing;


// Multiblocks

// T1 Drill
var loc = "tier_one_drill_base";
var meta = 2001;

var tier_one_drill_base = Builder.start(loc, meta)
    .withPattern(
        FactoryBlockPattern.start(RelativeDirection.RIGHT, RelativeDirection.BACK, RelativeDirection.UP)
            .aisle(
                "F~F",
                "~C~",
                "F~F")
            .aisle(
                "F~F",
                "~C~",
                "F~F")
            .aisle(
                "III",
                "IBI",
                "IEI")
			.aisle(
                "SSS",
                "SSS",
                "SSS")
            .aisleRepeatable(2,
                "~~~",
                "~B~",
                "~~~")
            .whereOr("I",
                <minecraft:cobblestone>,
                 IBlockMatcher.abilityPartPredicate( 
                    MultiblockAbility.IMPORT_FLUIDS,
                    MultiblockAbility.IMPORT_ITEMS,
                    MultiblockAbility.EXPORT_ITEMS
                ))
            .where("E", IBlockMatcher.controller(loc))
            .where("~", IBlockMatcher.AIR)
			.where("F", <gregtech:frame_wood>)
			.where("C", <minecraft:cobblestone_wall>)
			.where("S", <minecraft:stone_stairs>)
			.where("B", <minecraft:cobblestone>)
            .build())
    .addDesign(
        FactoryMultiblockShapeInfo.start()
            .aisle(
                "F F",
                "F F",
				"IEO",
				"SSS",
				"   ",
				"   ")
            .aisle(
                " C ",
                " C ",
				"BBX",
				"SBS",
				" B ",
				" B ")
            .aisle(
                "F F",
                "F F",
				"BBB",
				"SSS",
				"   ",
				"   ")
            .where("S", IBlockInfo.controller(loc))
            .where("I", MetaTileEntities.ITEM_IMPORT_BUS[0], IFacing.west())
            .where("O", MetaTileEntities.ITEM_EXPORT_BUS[0], IFacing.west())
            .where("X", MetaTileEntities.FLUID_IMPORT_HATCH[0], IFacing.west())
            .where("E", IBlockInfo.controller(loc))
			.where(" ", IBlockInfo.EMPTY)
			.where("F", <gregtech:frame_wood>)
			.where("C", <minecraft:cobblestone_wall>)
			.where("S", <minecraft:stone_stairs>)
			.where("B", <minecraft:cobblestone>)
            .build())
    .clearTooltips(true)
    .withRecipeMap(
        FactoryRecipeMap.start(loc)
		    .minInputs(1)
		    .maxInputs(1)
		    .minOutputs(3)
		    .maxOutputs(27)
		    .maxFluidInputs(1)
		    .build())
    .buildAndRegister();

// These are best specified in .lang files, since these may not work properly.
game.setLocalization(
    "multiblocktweaker.machine.tier_one_drill_base.name",
    "Tier one drill base controller"
);
game.setLocalization(
    "multiblocktweaker.multiblock.tier_one_drill_base.description",
    "i forgor"
);

// Don't forget to add a recipe!
recipes.addShaped(
    <gregtech:machine:2000>,
    [
        [<gregtech:cable:71>,         <gregtech:meta_item_2:32487>,         <gregtech:cable:71>],
        [<gregtech:meta_item_2:32487>, <gregtech:metal_casing:2>,  <gregtech:meta_item_2:32487>],
        [<gregtech:cable:71>,         <gregtech:meta_item_2:32487>,         <gregtech:cable:71>]
    ]
);

<multiblock:multiblocktweaker:tier_one_drill_base> // The Bracket Handler can also be used to refer to it
    .recipeMap
	.recipeBuilder()
    .duration(500)
    .inputs(<minecraft:chest>)
    .fluidInputs(<liquid:water> * 8000)
    .outputs(<gregtech:ore_cassiterite_0:3> * 64,
             <gregtech:ore_redstone_0> * 64,
             <gregtech:ore_nickel_0> * 64,
             <gregtech:ore_rutile_0> * 64,
             <gregtech:ore_rutile_0> * 64,
             <gregtech:ore_uraninite_0:3> * 64,
             <gregtech:ore_galena_0> * 64,
             <gregtech:ore_galena_0> * 64,
             <gregtech:ore_salt_0> * 64)
    .buildAndRegister();